plugins {
    id 'java'
    id 'org.springframework.boot' version "${spring_boot_version}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.openapi.generator' version '4.3.1'
    id 'org.liquibase.gradle' version '2.0.2'
    id 'com.google.cloud.tools.jib' version '2.3.0'
}

apply from: 'gradle/swagger.gradle'
apply from: 'gradle/docker.gradle'

group = 'com.hexagonal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
def generated = 'src/main/generated'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring boot starters
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // lombokc
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // H2
    runtimeOnly 'com.h2database:h2'

    // MySQL
    runtimeOnly 'mysql:mysql-connector-java'

    // OpenApi Bug workaround @see https://github.com/OpenAPITools/openapi-generator/issues/3668
    implementation "io.swagger:swagger-annotations:1.5.17"

    // mapstruct
    implementation "org.mapstruct:mapstruct:${mapstruct_version}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"

    // jackson
    implementation 'org.openapitools:jackson-databind-nullable:0.2.0'

    // queryDSL
    implementation "com.querydsl:querydsl-core:${querydsl_version}"
    implementation "com.querydsl:querydsl-jpa:${querydsl_version}"
    implementation "com.querydsl:querydsl-apt:${querydsl_version}"
    annotationProcessor(
            "com.querydsl:querydsl-apt:${querydsl_version}:jpa",
            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:${hibernate_jpa_api_version}",
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.projectlombok:lombok'
    )

    // liquibase
    implementation 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
    liquibaseRuntime 'mysql:mysql-connector-java'
    liquibaseRuntime 'ch.qos.logback:logback-core'
    liquibaseRuntime 'ch.qos.logback:logback-classic'
    liquibaseRuntime sourceSets.main.output

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'com.h2database:h2'
    testImplementation 'it.ozimov:embedded-redis:0.7.2'
    testImplementation "org.assertj:assertj-core:3.12.2"
}

test {
    useJUnitPlatform()
    systemProperty('spring.profiles.active', 'test')
}
